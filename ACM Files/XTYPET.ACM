**	$TYPET - TYPE TEXT.
*
*	$TYPET IS CALLED TO TYPE A BLOCK OF TEXT ON THE SYSTEM CONSOLE
*	AT TASK TIME RATHER THAN AT INTERRUPT TIME.
*
*	IMBEDDED ZERO BYTES INDICATE A CARRIAGE RETURN LINE FEED,
*	A BYTE WITH THE 200Q BIT SET IS THE LAST BYTE OF THE MESSAGE.
*
*	This routine modified to accomodate H8-4 ports by G.Chandler, 1-SEP-78
*	This routine assumes that the ports have been previously initialized,
*	and that S.CDB has been previously initialized.
*
*	Modified to use local version of S.CDB (SS.CDB)
*					gfr 8/23/2011
*
*	ENTRY	(RET) = TEXT
*	EXIT	TO (RET+LENGTH)
*	USES	A,F


$TYPET	XTHL			(HL) = TEXT ADDRESS
	CALL	$TYPET.		TYPE IT
	XTHL
	RET

$TYPET. MOV	A,M
	ANI	177Q
	CNZ	$TYPEC.		IF NOT CRLF
	ANA	A
	CZ	$TYPET1		IS CRLF
	CMP	M
	INX	H
	RNE			WAS 200 BIT SET
	JMP	$TYPET.

*	TYPE CRLF

$TYPET1 CALL	$TYPET
	DB	CR,LF+200Q
	XRA	A		RESTORE (A)
	RET
**	$TYPEC. - TYPE SINGLE CHARACTER.
*
*	IF CR, PADD WITH 4 ZERO BYTES
*
*	ENTRY	(A) = CHARACTER
*	EXIT	(A) = CHARACTER
*	USES	A,F


$TYPEC. PUSH	PSW		SAVE CHAR
	LDA	SS.CDB
	CPI	CDB.H84
	JZ	TYPEC2		IF H8-4 PORT

*	HAVE 8251 PORT FOR CONSOLE

TYPEC1	IN	SC.UART+USR
	ANI	USR.TXR
	JZ	TYPEC1		NOT READY
	POP	PTT
	OUT	SC.UART+UDR
	JMP	TYPEC3

*	HAVE 8250 PORT FOR CONSOLE

TYPEC2	IN	SC.ACE+UR.LSR
	ANI	UC.THE
	JZ	TYPEC2		NOT READY
	POP	PSW
	OUT	SC.ACE+UR.THR

TYPEC3	CPI	CR
	RNE			NOT CR

*	IS CR, PADD 4 TIMES

	MVI	A,4
TYPEC4	PUSH	PSW
	XRA	A
	CALL	$TYPEC.
	POP	PSW
	DCR	A
	JNZ	TYPEC4
	MVI	A,CR
	RET

