**	SCU - SETUP CONSOLE USART.
*
*	SCU CONFIGURES THE CONSOLE USART.
*
*	IF  8250
*	  THEN	PORT = 372-3Q
*	  ELSE	PORT = 340-7Q
*
*
*	EXIT	NONE
*	ENTRY	NONE
*	USES	A,F,(BC),(HL)
*
*	Modified to use local copy of S.CDB (SS.CDB),
*	S.CONTY (X.CONTY), and S.BAUD (X.BAUD)
*					gfr 8/23/2011
*

SCU	LDA	SS.CDB
	CPI	CDB.H84
	JZ	SCU1			IF  8250

*	PRESET 8251

	MVI	A,201Q
	OUT	SC.UART+USR		GET USART IN KNOWN STATE
	OUT	SC.UART+USR
	OUT	SC.UART+USR
	OUT	SC.UART+USR
	MVI	A,UCI.IR		RESET
	OUT	SC.UART+USR
	LDA	X.CONTY
	ANI	CTP.2SB
	ERRNZ	CTP.2SB*16+UMI.1B-UMI.2B
	RLC
	RLC
	RLC
	RLC
	ORI	UMI.1B+UMI.L8+UMI.16X
	OUT	SC.UART+USR
	MVI	A,UCI.ER+UCI.RE+UCI.TE
	OUT	SC.UART+USR
	RET

*	IS 8250

SCU1	IN	SC.ACE+UR.LSR					/80.01.GC/
	ANI	UC.TSE		CHECK FOR SHIFT EMPTY		/80.01.GC/
	JZ	SCU1

	XRA	A						/79.01.GC/
	OUT	SC.ACE+UR.IER	TURN OFF ANY INTERRUPTS		/79.01.GC/
	MVI	A,UC.LOO					/79.01.GC/
	OUT	SC.ACE+UR.MCR					/79.01.GC/
	LHLD	X.BAUD
	MVI	A,UC.DLA
	OUT	SC.ACE+UR.LCR	ACCESS DIVISOR LATCHES
	MOV	A,L
	OUT	SC.ACE+UR.DLL	SET LEAST SIGNIFICANT
	MOV	A,H
	ANI	177Q		TRIM STOP BITS
	OUT	SC.ACE+UR.DLM	SET MOST SIGNIFICANT
	LDA	X.CONTY
	ANI	CTP.2SB
	RRC
	ERRNZ	CTP.2SB/2-UC.2SB
	ERRNZ	UC.2SB-4	(A) = UC.2SB IF 2 STOP BITS
	ORI	UC.8BW		8 BIT WORDS
	OUT	SC.ACE+UR.LCR
	MVI	A,AC.DLY					/79.01.GC/
	CALL	.DLY						/79.01.GC/
	IN	SC.ACE+UR.RBR  GOBBLE ANY TRASH			/79.01.GC/
	IN	SC.ACE+UR.MCR					/79.01.GC/
	ANI	377Q-UC.LOO					/79.01.GC/
	OUT	SC.ACE+UR.MCR					/79.01.GC/
	RET
